plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group 'dev.jab125'
version '0.1.0'

repositories {
    mavenCentral()
    maven {
        url = "https://maven.covers1624.net/" // for DiffPatch
    }
}

dependencies {
    implementation 'codechicken:DiffPatch:1.5.0.29' // diffpatch uses java 8 so we're good
    implementation 'com.google.code.gson:gson:2.8.0' // use old version of GSON for maximum support.
    implementation 'com.jayway.jsonpath:json-path:2.8.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

shadowJar {
    dependencies {
        exclude(dependency('com.google.code.gson:gson:.*'))
        exclude(dependency('it.unimi.dsi:fastutil:.*'))
    }
    relocate "codechicken.diffpatch", "dev.jab125.configfixer.impl.shaded.diffpatch"
    relocate "com.jayway.jsonpath", "dev.jab125.configfixer.impl.shaded.jsonpath"
    relocate "net.covers1624.quack", "dev.jab125.configfixer.impl.shaded.quack"
    relocate "net.minidev.asm", "dev.jab125.configfixer.impl.shaded.asm"
    relocate "net.minidev.json", "dev.jab125.configfixer.impl.shaded.json"
    relocate "joptsimple", "dev.jab125.configfixer.impl.shaded.joptsimple"
    relocate "org", "dev.jab125.configfixer.impl.shaded.org"

    mergeServiceFiles()

    exclude "LICENSE.txt"
    exclude "META-INF/maven/**"
    exclude "META-INF/org/**"
    exclude "META-INF/LICENSE.txt"
    exclude "META-INF/NOTICE.txt"
}

test {
    useJUnitPlatform()
}

targetCompatibility = "1.8"
sourceCompatibility = "1.8"